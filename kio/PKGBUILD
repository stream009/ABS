# $Id: PKGBUILD 278536 2016-10-13 06:15:15Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.27.0
pkgrel=1
pkgdesc='Resource and network access abstraction'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('solid' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet' 'desktop-file-utils' 'kinit')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
groups=('kf5' 'modified')
install=kio.install
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('SKIP')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver

  for p in ../../patches/*; do
    patch -p1 < $p
  done
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
