# $Id: PKGBUILD 284152 2016-12-17 10:06:08Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=16.12.0
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork', 'modified')
depends=('kdebase-runtime' 'libmms')
makedepends=('cmake' 'automoc4' 'boost' 'libktorrent4' 'ragel')
optdepends=('libktorrent4: torrent support')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kget-${pkgver}.tar.xz"{,.sig})
sha1sums=('6967a1c83c12efb480e49b8260373e007c3a838f'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  pushd kget-$pkgver
  for p in ../../patches/*; do
    patch -p1 < $p
  done
  popd

  mkdir -p build
}

build() {
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_NepomukWidgets=OFF \
    -DWITH_KDE4Workspace=OFF \
    -DWITH_QGpgme=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
