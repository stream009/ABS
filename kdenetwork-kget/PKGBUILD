# $Id: PKGBUILD 259786 2016-02-16 18:06:13Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=15.12.3
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork', 'modified')
depends=('kdebase-runtime' 'kdebase-lib' 'libktorrent' 'libmms' 'kdepimlibs4')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kget-${pkgver}.tar.xz")
sha1sums=('bace6e2a6765985d49548c46670a565d6d9008b4')

prepare() {
  pushd kget-$pkgver
  for p in ../../patches/*; do
    patch -p1 < $p
  done
  popd

  mkdir -p build
}

build() {
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_NepomukWidgets=OFF \
    -DWITH_KDE4Workspace=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
